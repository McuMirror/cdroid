find_package(PkgConfig)
pkg_check_modules(PC_ZLIB  zlib)

find_path(ZLIB_INCLUDE_DIRS
    NAMES zlib.h
    HINTS ${PC_ZLIB_INCLUDEDIR}
    ${PC_ZLIB_INCLUDE_DIRS}
)

find_library(ZLIB_LIBRARIES
    NAMES z zlib
    HINTS ${PC_ZLIB_LIBDIR}
    ${PC_ZLIB_LIBRARY_DIRS}
)

#message("ZLIB_LIBRARIES=${ZLIB_LIBRARIES} ZLIB_INCLUDE_DIRS=${ZLIB_INCLUDE_DIRS} PC_ZLIB_VERSION=${PC_ZLIB_VERSION}")
if(ZLIB_INCLUDE_DIRS AND ZLIB_LIBRARIES)
    set(ZLIB_FOUND TRUE)
    set(ZLIB_LIBRARY ${ZLIB_LIBRARIES})
    set(ZLIB_INCLUDE_DIR ${ZLIB_INCLUDE_DIRS})
    set(ZLIB_VERSION ${PC_ZLIB_VERSION})

    if(NOT TARGET ZLIB::ZLIB)
      add_library(ZLIB::ZLIB UNKNOWN IMPORTED)
      set_target_properties(ZLIB::ZLIB PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES "${ZLIB_INCLUDE_DIRS}")

      if(ZLIB_LIBRARY_RELEASE)
        set_property(TARGET ZLIB::ZLIB APPEND PROPERTY
          IMPORTED_CONFIGURATIONS RELEASE)
        set_target_properties(ZLIB::ZLIB PROPERTIES
          IMPORTED_LOCATION_RELEASE "${ZLIB_LIBRARY_RELEASE}")
      endif()

      if(ZLIB_LIBRARY_DEBUG)
        set_property(TARGET ZLIB::ZLIB APPEND PROPERTY
          IMPORTED_CONFIGURATIONS DEBUG)
        set_target_properties(ZLIB::ZLIB PROPERTIES
          IMPORTED_LOCATION_DEBUG "${ZLIB_LIBRARY_DEBUG}")
      endif()

      if(NOT ZLIB_LIBRARY_RELEASE AND NOT ZLIB_LIBRARY_DEBUG)
        set_property(TARGET ZLIB::ZLIB APPEND PROPERTY
          IMPORTED_LOCATION "${ZLIB_LIBRARY}")
      endif()
    endif()
endif()

include(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(ZLIB FOUND_VAR ZLIB_FOUND VERSION_VAR ZLIB_VERSION 
	REQUIRED_VARS ZLIB_INCLUDE_DIRS ZLIB_INCLUDE_DIR ZLIB_LIBRARIES ZLIB_LIBRARY)

mark_as_advanced(ZLIB_LIBRARIES ZLIB_LIBRARY ZLIB_INCLUDE_DIRS ZLIB_INCLUDE_DIR ZLIB_FOUND ZLIB_VERSION)

